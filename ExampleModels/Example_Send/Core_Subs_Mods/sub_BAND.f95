SUBROUTINE BAND(J)
! BAND(J) computes delC and calls MATINV to invert B matrix using gaussian elimination.
use user_input, only: N, NJ
use variables, only: A, B, delC, D, G, X, Y, E
implicit none

integer :: j, M
real :: determ

    ! '''
    !     Outputs the arrays ğ„, ğ›, and ğ±
    !     From the block tridiagonal matrices ğ€, ğ, ğƒ, ğ—, ğ˜ and ğ†
    !
    !     BAND at j = 1
    !     ğğœâ±¼ + ğƒğœâ±¼â‚Šâ‚ + ğ—ğœâ±¼â‚Šâ‚‚ = ğ†
    !     ğ„â±¼ = ğâ»Â¹ ğƒ
    !     ğ›â±¼ = ğâ»Â¹ ğ†
    !     ğ±  = ğâ»Â¹ ğ—
    !
    !     BAND at j = 2
    !     ğ€ğœâ±¼â‚‹â‚ + ğğœâ±¼ + ğƒğœâ±¼â‚Šâ‚ = ğ†
    !     ğ„â±¼ = -[ğ + ğ€ğ„â±¼â‚‹â‚]â»Â¹ [ğ€ğ± + ğƒ]
    !     ğ›â±¼ =  [ğ + ğ€ğ„â±¼â‚‹â‚]â»Â¹ [ğ† - ğ€ğ›â±¼â‚‹â‚]
    !
    !     BAND at 2 < j < NJ
    !     ğ€ğœâ±¼â‚‹â‚ + ğğœâ±¼ + ğƒğœâ±¼â‚Šâ‚ = ğ†
    !     ğ„â±¼ = -[ğ + ğ€ğ„â±¼â‚‹â‚]â»Â¹ ğƒ
    !     ğ›â±¼ =  [ğ + ğ€ğ„â±¼â‚‹â‚]â»Â¹ [ğ† - ğ€ ğ›â±¼â‚‹â‚]
    !
    !     BAND at j = NJ
    !     ğ˜ğœâ±¼â‚‹â‚‚ + ğ€ğœâ±¼â‚‹â‚ + ğğœâ±¼ = ğ†
    !     ğœâ±¼ = -[ğ€ + [ğ˜ğ„â±¼â‚‹â‚‚] ğ„â±¼â‚‹â‚ + ğ]â»Â¹ [ğ† - ğ˜ğ›â±¼â‚‹â‚‚ - [ğ€ + ğ˜ğ„â±¼â‚‹â‚‚] ğ›â±¼â‚‹â‚ ]
    !     ğ€ <- ğ€ + ğ˜ğ„â±¼â‚‹â‚‚
    !     ğ† <- ğ† - ğ˜ğ›â±¼â‚‹â‚‚
    !     ğœâ±¼ = -[ğ + ğ€ ğ„â±¼â‚‹â‚]â»Â¹ [ğ† - ğ€ ğ›â±¼â‚‹â‚ ]
    !
    !     Calculate ğœâ±¼
    !     ğœâ±¼ = ğ›â±¼
    !     ğœâ±¼ = ğœâ±¼ + ğ„â±¼ğœâ±¼â‚Šâ‚  (j < NJ)
    !     ğœâ‚ = ğœâ‚ + ğ±ğœâ‚ƒ
    ! '''

101   FORMAT(15H DETERM=0 AT J=,I4)

      if (j == 1) then
        D(:, 2*N+1)   = G
        D(:, N+1:2*N) = X


        ! CALL MATINV(N, 2*N+1, DETERM, B, D)
        CALL MATINV(N, 2*N+1, DETERM)
        if (DETERM == 0.0) PRINT 101, J


        E(:, N+1, j) =  D(:, 2*N+1)                                             ! C.18
        E(:, 1:N, j) = -D(:, 1:N)                                               ! C.19
        X = -D(:, N+1:2*N)                                                      ! C.20
        RETURN

      else if (j == 2) then
        D(:, 1:N) = D(:, 1:N) + matmul(A, X)

      else if (j == NJ) then
        G = G - matmul(Y, E(:, N+1, j-2))                                       ! C.27
        A = A + matmul(Y, E(:, 1:N, j-2))                                       ! C.28
      end if


      ! ############### if j /= 1
      D(:, N+1) = -G + matmul( A, E(:, N+1, j-1) )                              ! C.23
      B         =  B + matmul( A, E(:, 1:N, j-1) )                              ! C.25


      ! CALL MATINV(N, N+1, DETERM, B, D)     ! returns D = matmul(B^-1, D)
      CALL MATINV(N, N+1, DETERM)

      if (DETERM == 0.0) PRINT 101, J

      E(:,:,j) = -D(:,1:N+1)

      if (j == NJ) then
        delC(:,j) = E(:, N+1, j)      ! delC(:,:) = E(:, N+1, :)                !

        do M = NJ-1, 1, -1
          delC(:,M) = E(:, N+1, M) + matmul(E(:, 1:N, M), delC(:,M+1))          ! C.22
        end do

        delC(:,1) = delC(:,1) + matmul(X, delC(:,3))                            ! Appendix C.17
      end if

      RETURN
END SUBROUTINE BAND
